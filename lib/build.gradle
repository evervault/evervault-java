plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'maven-publish'
    id 'signing'
}

group 'com.evervault'
version '3.3.2'

repositories {
    mavenCentral()
    mavenLocal()
}

def getKeyFile() {
    return hasProperty("signingKey") ? signingKey : ""
}

def getKeyPassword() {
    return hasProperty("signingPassword") ? signingPassword : ""
}

dependencies {
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.10.+'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.+'
    testImplementation "org.mockito:mockito-core:5.6.+"
    testImplementation "org.mock-server:mockserver-netty:5.14.+"

    implementation 'com.google.code.gson:gson:2.10.+'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Evervault'
                description = 'Evervault SDK enabling developers to integrate their applications with Evervault services'
                url = 'https://github.com/evervault/evervault-java'
                scm {
                    url = 'https://github.com/evervault/evervault-java'
                    connection = 'scm:git:ssh://git@github.com:evervault/evervault-java.git'
                }
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org/'
                    }
                }
                developers {
                    developer {
                        name = 'engineering'
                        organization = 'Evervault'
                        email = 'engineering@evervault.com'
                    }
                }
                organization {
                    name = 'Evervault'
                    url = 'https://www.evervault.com/'
                }
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(getKeyFile(), getKeyPassword())
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}