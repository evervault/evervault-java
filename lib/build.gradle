plugins {
    id 'java-library'
    id 'jvm-test-suite'
    id 'maven-publish'
    id 'signing'
}

group 'com.evervault'
version '3.3.0'

repositories {
    mavenCentral()
    mavenLocal()
}

def getRepositoryUsername() {
    return hasProperty("ossrhUsername") ? ossrhUsername : ""
}

def getRepositoryPassword() {
    return hasProperty("ossrhPassword") ? ossrhPassword : ""
}

def getKeyFile() {
    return hasProperty("signingKey") ? signingKey : ""
}

def getKeyPassword() {
    return hasProperty("signingPassword") ? signingPassword : ""
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'
    testImplementation "com.github.tomakehurst:wiremock-jre8:2.32.0"
    testImplementation "org.mockito:mockito-core:3.+"

    implementation 'com.google.code.gson:gson:2.8.9'
    implementation 'com.google.guava:guava:31.1-android'
    implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'

}

java {
    withJavadocJar()
    withSourcesJar()
}

testing {
    suites {
        test {
            useJUnitJupiter()
        }
        integrationTests(JvmTestSuite) {
            dependencies {
                implementation project
                implementation group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.70'
                implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Evervault'
                description = 'Evervault SDK enabling developers to integrate their applications with Evervault services'
                url = 'https://github.com/evervault/evervault-java'
                scm {
                    url = 'https://github.com/evervault/evervault-java'
                    connection = 'scm:git:ssh://git@github.com:evervault/evervault-java.git'
                }
                licenses {
                    license {
                        name = 'The MIT License (MIT)'
                        url = 'https://mit-license.org/'
                    }
                }
                developers {
                    developer {
                        name = 'engineering'
                        organization = 'Evervault'
                        email = 'engineering@evervault.com'
                    }
                }
                organization {
                    name = 'Evervault'
                    url = 'https://www.evervault.com/'
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            def releasesRepoUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            def snapshotsRepoUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username getRepositoryUsername()
                password getRepositoryPassword()
            }
        }
    }
}

signing {
    useInMemoryPgpKeys(getKeyFile(), getKeyPassword())
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}